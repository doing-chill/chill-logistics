plugins {
	id 'java'
	id 'org.springframework.boot' version '3.5.7' apply false
	id 'io.spring.dependency-management' version '1.1.7' apply false
}

ext {
	springCloudVersion = "2025.0.0"
	springBootVersion = "3.5.7"
}

jar{
	enabled = false
}

group = 'chill.logistics'
version = '0.0.1-SNAPSHOT'

subprojects {
	group = rootProject.group
	version = rootProject.version

	apply plugin: 'java'
	apply plugin: 'io.spring.dependency-management'


	java {
		toolchain {
			languageVersion = JavaLanguageVersion.of(17)
		}
	}

	repositories {
		mavenCentral()
	}

	dependencyManagement {
		imports {
			mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
			mavenBom "org.springframework.boot:spring-boot-dependencies:${springBootVersion}"
		}
	}

	dependencies {
		compileOnly 'org.projectlombok:lombok:1.18.32'
		annotationProcessor 'org.projectlombok:lombok:1.18.32'
		implementation 'jakarta.persistence:jakarta.persistence-api'
	}
}


configure([
		project(":eureka-server"),
		project(":gateway-server"),
		project(":user-server"),
		project(":firm-server"),
		project(":hub-server"),
		project(":product-server"),
		project(":order-server"),
		project(":delivery-server"),
		project(":external-server")
]) {
	apply plugin: 'org.springframework.boot'
}


